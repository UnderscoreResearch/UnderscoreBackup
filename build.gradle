import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'org.beryx.runtime' version '1.12.7'
}

application {
    mainClass = 'com.underscoreresearch.backup.cli.Main'
    applicationDefaultJvmArgs = ['-Xmx256m']
}

group 'com.underscoreresearch'
version '1.2.0rc3'

sourceCompatibility = 17
targetCompatibility = 17

sourceSets {
    main {
        java {
            srcDirs 'src/main'
        }
        resources {
            srcDirs 'src/resources'
        }
    }
    test {
        java {
            srcDirs 'src/test'
        }
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = rootProject.version.toString()
            p['edition'] = ""
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.logging',
               'java.sql',
               'java.desktop',
               'java.xml',
               'jdk.unsupported',
               'java.security.jgss',
               'java.rmi',
               'java.naming',
               'java.compiler',
               'java.scripting',
               'java.management',
               'jdk.compiler',
               'java.instrument',
               'jdk.crypto.ec']
    jpackage {
        appVersion = "${rootProject.version}".replaceAll('(pre|rc)\\d+$', "")
        installerName = 'underscorebackup'
        if (project.properties['installer_type']) {
            if (project.property("installer_type").equals("win-console")) {
                installerType = "exe"
                imageOptions = ["--icon", "${rootProject.projectDir}/windows/application.ico", "--win-console"]
            } else if (project.property("installer_type").equals("win-gui")) {
                installerType = "exe"
                imageName = "underscorebackup-gui"
                imageOptions = ["--icon", "${rootProject.projectDir}/windows/application.ico", "--arguments", "interactive"]
            } else {
                installerType = project.property("installer_type")
            }
        }
    }
}

tasks.named("compileJava").get().dependsOn("webui")

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked']
}

task installerDef(type: Copy) {
    from("${rootProject.projectDir}/windows/innosetup.iss")
    rename("innosetup.iss", "innosetup-expanded.iss")
    expand([
            applicationVersion: "${rootProject.version}"
    ])
    into("${rootProject.projectDir}/windows")
}

task installerImage(type: Exec) {
    workingDir "${rootProject.projectDir}"
    commandLine 'cmd', '/c',
            'gradlew.bat jpackageImage -Pinstaller_type=win-console ' +
                    ' && ( rmdir /s /q build\\installerimage || ver > nul )' +
                    ' && mkdir build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\app build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\runtime build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\README.md build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\underscorebackup.exe build\\installerimage' +
                    ' && gradlew.bat jpackageImage -Pinstaller_type=win-gui' +
                    ' && copy build\\jpackage\\underscorebackup-gui\\underscorebackup-gui.exe build\\installerimage' +
                    ' && copy build\\jpackage\\underscorebackup-gui\\app\\underscorebackup-gui.cfg build\\installerimage\\app'
}

task installer(dependsOn: [installerImage, installerDef], type: Exec) {
    workingDir "${rootProject.projectDir}/windows"
    executable 'C:\\Program Files (x86)\\Inno Setup 6\\iscc.exe'
    args "innosetup-expanded.iss"
}

task webuiinstall(type: Exec) {
    onlyIf {
        !new File("src/main/resources/web/index.html").exists()
    }
    workingDir 'webui'
    commandLine 'npm', "install"
}

task webui(dependsOn: [webuiinstall], type: Exec) {
    onlyIf {
        !new File("src/main/resources/web/index.html").exists()
    }
    workingDir 'webui'
    commandLine 'npm', "run", "build"
}

repositories {
    mavenCentral()
}

distributions {
    main {
        distributionBaseName = 'underscorebackup'
        contents {
            from('.') {
                include 'README.md'
                include 'bin/*'
                include 'scripts/*'
            }
        }
    }
}

tasks.jpackageImage.doLast {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        copy {
            from "."
            include "README.md"
            into "build/jpackage/${rootProject.name}/"
        }
    }

}


dependencies {

    implementation(
            'com.dropbox.core:dropbox-core-sdk:5.4.4',
            'com.cronutils:cron-utils:9.2.0',
            'com.fasterxml.jackson.core:jackson-databind:2.14.0',
            'com.google.guava:guava:31.1-jre',
            'com.google.inject:guice:5.1.0',
            'com.hierynomus:smbj:0.11.5',
            'com.joestelmach:natty:0.13',
            'commons-codec:commons-codec:1.15',
            'commons-cli:commons-cli:1.5.0',
            'javax.xml.bind:jaxb-api:2.3.1',
            'org.apache.commons:commons-lang3:3.12.0',
            'org.apache.logging.log4j:log4j-api:2.19.0',
            'org.apache.logging.log4j:log4j-core:2.19.0',
            'org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0',
            'org.mapdb:mapdb:3.0.8',
            'org.projectlombok:lombok:1.18.24',
            'org.reflections:reflections:0.10.2',
            'org.slf4j:slf4j-api:2.0.4',
            'org.takes:takes:1.24.4',
            'software.amazon.awssdk:s3control:2.18.21'
    )

    testImplementation(
            'org.hamcrest:hamcrest:2.2',
            'org.junit.jupiter:junit-jupiter:5.9.0',
            'org.mockito:mockito-junit-jupiter:4.8.0'
    )

    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

test {
    useJUnitPlatform()
}

task installerPkg(dependsOn: ["runtime"], type: Exec) {
    workingDir "${rootProject.projectDir}"
    executable 'osx/createinstaller.sh'
    args "${rootProject.version}"
}

task uninstallerPkg(type: Exec) {
    workingDir "${rootProject.projectDir}"
    executable 'osx/createuninstaller.sh'
    args "${rootProject.version}"
}

task allDist() {
    dependsOn distZip, distTar
}

task installers(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir "${rootProject.projectDir}"
        commandLine 'gradlew.bat', "installer"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        dependsOn uninstallerPkg
        workingDir "${rootProject.projectDir}"
        commandLine './gradlew', "installerPkg"
    } else {
        workingDir "${rootProject.projectDir}"
        environment "VERSION", "${rootProject.version}"
        commandLine 'bash', '-c', "linux/builddeb.sh && linux/buildrpm.sh"
    }
}

task integrationTest(type: Exec) {
    dependsOn installers
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir "${rootProject.projectDir}"
        commandLine "perl", "integrationtests\\testsuite.pl", "$buildDir\\integrationtests", "$buildDir\\installerimage\\underscorebackup.exe", "${rootProject.projectDir}\\webui"
    } else {
        dependsOn distTar
        commandLine "${rootProject.projectDir}/integrationtests/executetests.sh", "$buildDir/distributions/${rootProject.name}-${rootProject.version}.tar", "${rootProject.projectDir}/webui"
    }
}

allDist.finalizedBy(installers)

task allDistTest {
    dependsOn test, integrationTest, allDist
}
