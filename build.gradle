import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'edu.sc.seis.launch4j' version '2.5.2'
    id "org.beryx.runtime" version "1.3.0"
}

application {
    mainClass = 'com.underscoreresearch.backup.cli.Main'
    applicationDefaultJvmArgs = ['-Xmx256m']
}

group 'com.underscoreresearch'
version '0.9.14'

sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
    main {
        java {
            srcDirs 'src/main'
        }
        resources {
            srcDirs 'src/resources'
        }
    }
    test {
        java {
            srcDirs 'src/test'
        }
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.logging',
               'java.sql',
               'java.desktop',
               'java.xml',
               'jdk.unsupported',
               'java.security.jgss',
               'java.rmi',
               'java.naming',
               'java.compiler',
               'java.scripting',
               'java.management',
               'jdk.compiler',
               'java.instrument',
               'jdk.crypto.ec']
}

tasks.named("compileJava").get().dependsOn("webui")

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked']
}

task copyIniFile(type:Copy) {
    from layout.projectDirectory.dir("windows/underscorebackup.l4j.ini")
    into layout.buildDirectory.dir("launch4j")
}

launch4j {
    priority = "idle"
    jreMinVersion = 11
    bundledJrePath = "..\\jre"
    bundledJre64Bit = true
    jvmOptions = ["-Dstartup.directory=\"%OLDPWD%\""]
    jreRuntimeBits = "64"
    mainClassName = 'com.underscoreresearch.backup.cli.Main'
    icon = "${rootProject.projectDir}/windows/application.ico"
}

task consoleexe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    dependsOn(copyIniFile)
    outfile = "underscorebackup.exe"
    headerType = "console"
}

task guiexe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    cmdLine = "interactive"
    outfile = "underscorebackup-gui.exe"
    headerType = "gui"
}

task installerDef(type: Copy) {
    from("${rootProject.projectDir}/windows/innosetup.iss")
    rename("innosetup.iss", "innosetup-expanded.iss")
    expand([
            applicationVersion: "${version}"
    ])
    into("${rootProject.projectDir}/windows")
}

task installer(dependsOn: [jre, guiexe, consoleexe, installerDef], type:Exec) {
    workingDir "${rootProject.projectDir}/windows"
    executable 'C:\\Program Files (x86)\\Inno Setup 6\\iscc.exe'
    args "innosetup-expanded.iss"
}

task webuiinstall(type:Exec) {
    onlyIf {
        !new File("src/main/resources/web/index.html").exists()
    }
    workingDir 'webui'
    commandLine 'npm', "install"
}

task webui(dependsOn: [webuiinstall], type:Exec) {
    onlyIf {
        !new File("src/main/resources/web/index.html").exists()
    }
    workingDir 'webui'
    commandLine 'npm', "run", "build"
}

task integrationTest(type:Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn consoleexe
        workingDir "${rootProject.projectDir}"
        commandLine "perl", "integrationtests\\testsuite.pl", "$buildDir\\integrationtests", "$buildDir\\launch4j\\underscorebackup.exe", "${rootProject.projectDir}\\webui"
    } else {
        dependsOn distTar
        commandLine "${rootProject.projectDir}/integrationtests/executetests.sh", "$buildDir/distributions/${project.name}-${project.version}.tar", "${rootProject.projectDir}/webui"
    }
}

repositories {
    mavenCentral()
}

distributions {
    main {
        distributionBaseName = 'underscorebackup'
        contents {
            from('.') {
                include 'README.md'
                include 'bin/*'
                include 'scripts/*'
            }
        }
    }
}

dependencies {

    implementation (
            'com.dropbox.core:dropbox-core-sdk:5.2.0',
            'com.cronutils:cron-utils:9.1.6',
            'com.fasterxml.jackson.core:jackson-databind:2.13.3',
            'com.google.guava:guava:31.1-jre',
            'com.google.inject:guice:5.1.0',
            'com.hierynomus:smbj:0.11.5',
            'com.joestelmach:natty:0.13',
            'commons-cli:commons-cli:1.5.0',
            'javax.xml.bind:jaxb-api:2.3.1',
            'org.apache.commons:commons-lang3:3.12.0',
            'org.apache.logging.log4j:log4j-api:2.17.2',
            'org.apache.logging.log4j:log4j-core:2.17.2',
            'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2',
            'org.mapdb:mapdb:3.0.8',
            'org.projectlombok:lombok:1.18.24',
            'org.reflections:reflections:0.10.2',
            'org.slf4j:slf4j-api:1.7.36',
            'org.takes:takes:1.19',
            'software.amazon.awssdk:s3control:2.17.191'
    )

    testImplementation (
            'org.hamcrest:hamcrest:2.2',
            'org.junit.jupiter:junit-jupiter:5.8.2',
            'org.mockito:mockito-junit-jupiter:4.5.1'
    )

    testRuntimeOnly (
            'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

test {
    useJUnitPlatform()
}

task installerPkg(dependsOn: ["runtime"], type:Exec) {
    workingDir "${rootProject.projectDir}"
    executable 'osx/createinstaller.sh'
    args "${project.version}"
}

task uninstallerPkg(type:Exec) {
    workingDir "${rootProject.projectDir}"
    executable 'osx/createuninstaller.sh'
    args "${project.version}"
}

task allDist() {
    dependsOn distZip, distTar
}

task installers(type:Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir "${rootProject.projectDir}"
        commandLine 'gradlew.bat', "installer"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        dependsOn uninstallerPkg
        workingDir "${rootProject.projectDir}"
        commandLine './gradlew', "installerPkg"
    } else {
        commandLine 'true'
    }
}

allDist.finalizedBy(installers)

task allDistTest {
    dependsOn test, integrationTest, allDist
}
