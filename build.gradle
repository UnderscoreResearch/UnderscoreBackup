import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'org.beryx.runtime' version '1.13.0'
}

application {
    mainClass = 'com.underscoreresearch.backup.cli.Main'
    applicationDefaultJvmArgs = [
//          '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n',
            '-Xmx256m',
            '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
    ]
}

group 'com.underscoreresearch'
version '2.2.0.5'
def releaseType = 'rc'

def archiveName = 'underscorebackup'
def edition = ''

sourceCompatibility = 17
targetCompatibility = 17

sourceSets {
    main {
        java {
            srcDirs 'src/main'
        }
        resources {
            srcDirs 'src/resources'
        }
    }
    test {
        java {
            srcDirs 'src/test'
        }
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = rootProject.version.toString() + releaseType
            p['edition'] = edition
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.logging',
               'java.sql',
               'java.desktop',
               'java.xml',
               'java.net.http',
               'jdk.unsupported',
               'java.security.jgss',
               'java.rmi',
               'java.naming',
               'java.compiler',
               'java.scripting',
               'java.management',
               'jdk.compiler',
               'java.instrument',
               'jdk.crypto.ec']
    jpackage {
        appVersion = "${rootProject.version}"
        installerName = archiveName
        if (project.properties['installer_type']) {
            if (project.property("installer_type") == "win-console") {
                installerType = "exe"
                imageOptions = ["--icon", "${rootProject.projectDir}/windows/application.ico", "--win-console"]
            } else if (project.property("installer_type") == "win-gui") {
                installerType = "exe"
                imageName = "underscorebackup-gui"
                imageOptions = ["--icon", "${rootProject.projectDir}/windows/application.ico", "--arguments", "interactive"]
            } else {
                installerType = project.property("installer_type")
            }
        }
    }
}

tasks.named("compileJava").get().dependsOn("webui")

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation', '-encoding', 'UTF-8']
}

task webuiinstall(type: Exec) {
    onlyIf {
        !new File("src/main/resources/web/index.html").exists()
    }
    workingDir 'webui'
    commandLine 'npm', "install"
}

task webui(dependsOn: [webuiinstall], type: Exec) {
    onlyIf {
        !new File("src/main/resources/web/index.html").exists()
    }
    workingDir 'webui'
    commandLine 'npm', "run", "build"
}

repositories {
    mavenCentral()
}

distributions {
    main {
        distributionBaseName = archiveName
        contents {
            from('.') {
                include 'README.md'
                include 'bin/*'
                include 'scripts/*'
            }
        }
    }
}

tasks.jpackageImage.doLast {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        copy {
            from "."
            include "README.md"
            into "build/jpackage/underscorebackup/"
        }
    }
}

tasks.distTar.doLast {
    ant.move file: "build/distributions/${archiveName}-${rootProject.version}.tar",
            tofile: "build/distributions/${archiveName}-${rootProject.version}${releaseType}.tar"
}

tasks.distZip.doLast {
    ant.move file: "build/distributions/${archiveName}-${rootProject.version}.zip",
            tofile: "build/distributions/${archiveName}-${rootProject.version}${releaseType}.zip"
}

dependencies {

    implementation(
            'com.cronutils:cron-utils:9.2.1',
            'com.dropbox.core:dropbox-core-sdk:5.4.5',
            'com.fasterxml.jackson.core:jackson-databind:2.15.2',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2',
            'com.google.guava:guava:32.0.0-jre',
            'com.google.inject:guice:7.0.0',
            'com.hierynomus:smbj:0.12.1',
            'org.lmdbjava:lmdbjava:0.8.3',
            'com.joestelmach:natty:0.13',
            'commons-cli:commons-cli:1.5.0',
            'commons-codec:commons-codec:1.16.0',
            'de.mkammerer:argon2-jvm:2.11',
            'io.swagger:swagger-annotations:1.6.11',
            'jakarta.annotation:jakarta.annotation-api:2.1.1',
            'javax.xml.bind:jaxb-api:2.3.1',
            'org.apache.commons:commons-lang3:3.12.0',
            'commons-net:commons-net:3.9.0',
            'org.apache.logging.log4j:log4j-api:2.20.0',
            'org.apache.logging.log4j:log4j-core:2.20.0',
            'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0',
            'org.mapdb:mapdb:3.0.9',
            'org.openapitools:jackson-databind-nullable:0.2.6',
            'org.projectlombok:lombok:1.18.28',
            'org.reflections:reflections:0.10.2',
            'org.slf4j:slf4j-api:2.0.7',
            'org.takes:takes:1.24.4',
            'software.amazon.awssdk:s3control:2.20.103'
    )

    testImplementation(
            'org.hamcrest:hamcrest:2.2',
            'org.junit.jupiter:junit-jupiter:5.9.3',
            'org.mockito:mockito-junit-jupiter:5.4.0'
    )

    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.20.0'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

test {
    useJUnitPlatform()

    doFirst {
        jvmArgs += [
                '--add-opens=java.base/java.nio=ALL-UNNAMED',
                '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
        ]
    }
}

task installerDef(type: Copy) {
    from("${rootProject.projectDir}/windows/innosetup.iss")
    rename("innosetup.iss", "innosetup-expanded.iss")
    expand([
            applicationVersion: "${rootProject.version}${releaseType}"
    ])
    into("${rootProject.projectDir}/windows")
}

task installerImage(type: Exec) {
    workingDir "${rootProject.projectDir}"
    commandLine 'cmd', '/c',
            'gradlew.bat jpackageImage -Pinstaller_type=win-console' +
                    ' && attrib -r build\\jpackage\\underscorebackup\\underscorebackup.exe' +
                    ' && perl windows\\signexe.pl optional build\\jpackage\\underscorebackup' +
                    ' && ( rmdir /s /q build\\installerimage || ver > nul )' +
                    ' && mkdir build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\app build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\runtime build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\README.md build\\installerimage' +
                    ' && move /Y build\\jpackage\\underscorebackup\\underscorebackup.exe build\\installerimage' +
                    ' && gradlew.bat jpackageImage -Pinstaller_type=win-gui' +
                    ' && attrib -r build\\jpackage\\underscorebackup-gui\\underscorebackup-gui.exe' +
                    ' && perl windows\\signexe.pl optional build\\jpackage\\underscorebackup-gui' +
                    ' && copy build\\jpackage\\underscorebackup-gui\\underscorebackup-gui.exe build\\installerimage' +
                    ' && copy build\\jpackage\\underscorebackup-gui\\app\\underscorebackup-gui.cfg build\\installerimage\\app'
}

task installerWindows(dependsOn: [installerImage, installerDef], type: Exec) {
    workingDir "${rootProject.projectDir}/windows"
    executable 'C:\\Program Files (x86)\\Inno Setup 6\\iscc.exe'
    args "innosetup-expanded.iss"
}

task installerLinux(type: Exec) {
    workingDir "${rootProject.projectDir}"
    environment "VERSION", "${rootProject.version}${releaseType}"
    commandLine 'bash', '-c', "linux/builddeb.sh && linux/buildrpm.sh"
}

task installerPkg(dependsOn: ["runtime"], type: Exec) {
    workingDir "${rootProject.projectDir}"
    executable 'osx/createinstaller.sh'
    args "${rootProject.version}${releaseType}"
}

task uninstallerPkg(type: Exec) {
    workingDir "${rootProject.projectDir}"
    executable 'osx/createuninstaller.sh'
    args "${rootProject.version}${releaseType}"
}

task installer {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn installerWindows
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        dependsOn uninstallerPkg, installerPkg
    } else {
        dependsOn installerLinux
    }
}

task allDist(type: Exec) {
    dependsOn installer
    workingDir "${rootProject.projectDir}"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "gradlew.bat", "distTar", "distZip"
    } else {
        commandLine "./gradlew", "distTar", "distZip"
    }
}

task integrationTest(type: Exec) {
    dependsOn allDist
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir "${rootProject.projectDir}"
        commandLine "perl", "integrationtests\\testsuite.pl", "$buildDir\\integrationtests", "$buildDir\\installerimage\\underscorebackup.exe", "${rootProject.projectDir}\\webui"
    } else {
        commandLine "${rootProject.projectDir}/integrationtests/executetests.sh", "$buildDir/distributions/${archiveName}-${rootProject.version}${releaseType}.tar", "${rootProject.projectDir}/webui"
    }
}

task allDistTest {
    dependsOn integrationTest, test
}
